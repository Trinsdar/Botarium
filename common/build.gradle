dependencies {
    // We depend on fabric loader here to use the fabric @Environment annotations and get the mixin dependencies
    // Do NOT use other classes from fabric loader
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    compileOnly "net.msrandom:class-extension-annotations:1.0"
}

architectury {
    common()
}

java {
    registerFeature(sourceSets.test.name) {
        usingSourceSet(sourceSets.test)
    }
}

components.java {
    withVariantsFromConfiguration(configurations.testApiElements) {
        skip()
    }
    withVariantsFromConfiguration(configurations.testRuntimeElements) {
        skip()
    }
}

def ENV = System.getenv()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact remapJar
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            artifactId = rootProject.mod_id + "-" + project.name + "-" + project.minecraft_version
            pom {
                name = 'Botarium'
                description = 'Tiny lil library mod for cross platform energy and registries'
                url = 'https://github.com/terrarium-earth/Spirit'

                scm {
                    connection = 'git:https://github.com/CodexAdrian/Botarium.git'
                    developerConnection = 'git:https://github.com/CodexAdrian/Botarium.git'
                    url = 'https://github.com/CodexAdrian/Botarium'
                }

                licenses {
                    license {
                        name = 'MIT'
                    }
                }
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        maven {
            url = "https://repo.repsy.io/mvn/trinsdar/gregtech-intergalactical/"
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}